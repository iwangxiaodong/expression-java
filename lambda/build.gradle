plugins {
    id 'java-library'
    id "com.jfrog.bintray" version "1.8.4"
}

//defaultTasks  'clean','build','uploadArchives'

sourceCompatibility = JavaVersion.VERSION_1_10

version '1.0.9'
group 'com.openle.module.lambda'

test {
    useJUnitPlatform()
}

// 兼容中文注释和排除module-info.java
tasks.withType(Javadoc) {
    options.encoding = "UTF-8"
    title =title+" - Open Source"
    excludes = ['module-info.java','**/jinq/**', '**/jooq/**']
    //failOnError = false
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs = ['-Xlint:unchecked']   
}


configurations {
    deployerJars
}

dependencies {
    
    deployerJars group: 'org.apache.maven.wagon', name: 'wagon-webdav-jackrabbit', version: '3.0.0'
        
    compile fileTree(include: ['*.jar'], dir: 'libs')

    compile('com.openle.module:core:1.0.6')
    compile group: 'net.bytebuddy', name: 'byte-buddy', version: '1.8.12'

    //for lambda to sql
    //compile group: 'org.jinq', name: 'jinq-jooq', version: '1.8.22'
    compile group: 'org.jooq', name: 'jooq', version: '3.11.1'  //  '3.10.2'
    compile group: 'org.jinq', name: 'analysis', version: '1.8.24'  //  '1.8.22'

    //for method ref
    compile "org.mockito:mockito-core:+"
    
    compile group: 'org.ow2.asm', name: 'asm-util', version: '6.2.1'
        
    testCompile("org.junit.jupiter:junit-jupiter-api:5.3.1")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.3.1")
}

compileJava {
    doFirst {
        options.compilerArgs = [
                    '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

apply plugin: 'maven'
uploadArchives {
    repositories {
        mavenDeployer {
            pom.artifactId =project.archivesBaseName.toLowerCase()
            configuration = configurations.deployerJars

            // Win10重启后环境变量方有效 echo %WEBDAV_PASSWORD%
            ext.pwd = project.hasProperty('WEBDAV_PASSWORD') ? project.property('WEBDAV_PASSWORD') : System.getenv('WEBDAV_PASSWORD')
            //println ext.pwd
            repository(url: "dav:http://all.openle.com:30580/repository/") {
                authentication(userName: "webdav", password: ext.pwd)
            }
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// ./gradlew build bintrayUpload -PBINTRAY_API_KEY=myKey  //or 设定环境变量: export BINTRAY_API_KEY=myKey
bintray {
    user = project.hasProperty('BINTRAY_USER') ? project.property('BINTRAY_USER') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('BINTRAY_API_KEY') ? project.property('BINTRAY_API_KEY') : System.getenv('BINTRAY_API_KEY')
    //println key
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'lambda'
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/iwangxiaodong/expression-java.git'
        version {
            name = project.version.toString()
            desc = 'Lambda'
        }
    }
}